defaultTasks 'run'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version '1.8'
group   'org.jason-lang'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    google()

    maven { url "http://jacamo.sourceforge.net/maven2" }
    maven { url "https://raw.github.com/rosjava/rosjava_mvn_repo/master" }

    flatDir {
       dirs 'lib'
    }
}

dependencies {
    compile group: 'org.jason-lang',     name: 'jason' ,   version: '2.5-SNAPSHOT'
    compile 'org.ros.rosjava_core:rosjava:[0.3,0.4)'
    compile 'org.ros.rosjava_messages:std_msgs:[0.5,0.6)'
    compile 'org.ros.rosjava_bootstrap:message_generation:[0.3,0.4)'
    compile files('lib/jason_ros_msgs.jar') //(name:'jason_ros_msgs', ext:'jar')

    compile 'com.google.guava:guava:12.0'
    compile 'org.apache.commons:com.springsource.org.apache.commons.logging:1.1.1'

}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

task run (type: JavaExec, dependsOn: 'classes') {
    description 'runs the application'
    main = 'jason.infra.centralised.RunCentralisedMAS'
    args 'rosjava_agents.mas2j'
    classpath sourceSets.main.runtimeClasspath
}

clean {
    delete 'bin'
    delete 'build'
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    baseName 'jasonros'
    classifier = 'javadoc'
    //from '${docsDir}/../../doc/api'
}

task sourceJar (type : Jar) {
    baseName 'jasonros'
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar, javadocJar
}


publishing {
    publications {
        jasonros(MavenPublication) {
            from components.java
			      artifact sourceJar
			      artifact javadocJar
            artifactId 'jasonros'
        }
        jasonros_msgs(MavenPublication) {
            artifact 'lib/jason_ros_msgs.jar'
            artifactId 'jasonros_msgs'
        }
    }
}

task publishMavenGitHub(dependsOn: [ 'build', 'publishToMavenLocal']) {
  doLast {
    def wdir = System.getProperty("user.home")+'/.m2/repository/org/jason-lang/jasonros'
    def wdir_msgs = System.getProperty("user.home")+'/.m2/repository/org/jason-lang/jasonros_msgs'
    def rdir = System.getProperty("user.home")+'/pro/mvn-repo'
    exec {
        commandLine 'git', 'pull'
        workingDir rdir
    }
    copy {
        from wdir
        into rdir + '/org/jason-lang/jasonros'
    }
    copy {
        from wdir_msgs
        into rdir + '/org/jason-lang/jasonros_msgs'
    }
    exec {
        commandLine 'git', 'add', '*'
        workingDir rdir
    }
    exec {
        commandLine 'git', 'commit', '-a', '-m', 'new version of jasonros '+project.version
        workingDir rdir
    }
    exec {
        commandLine 'git', 'push'
        workingDir rdir
    }
  }
}
